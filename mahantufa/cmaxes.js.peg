//simplified morphology parser: zoi and non lojban text not supported. capital letters, illegal consonant pairs are preprocessed
{
  function _join(a)
  {
    if (typeof(a) == "string") return a; else {var r = ""; for (var v in a) { r += _join(a[v]); } return r;}
  }

  function _node_int(a)
  {
    if (typeof(a) == "string") return a;
    var r = [];
    for (var v in a)  {r.push( _node_int( a[v] ) );}return r;
  }
}

text = pause? expr:(any_word*) {return _node_int(expr);}
any_word = expr:(lojban_word) pause? {return expr;}
lojban_word = cmevla / expr:(cmavo) {return ["cmavo", _join(expr)];} / expr:(gismu) {return ["gismu", _join(expr)];} / expr:(!gismu !fuhivla !cmavo !(ini_vwl h y onset) lujvo_core) {return ["lujvo", _join(expr)];} / expr:(fuhivla) {return ["fu'ivla", _join(expr)];}

cmevla = expr:((&zifcme (any_syllable)* &pause) / zifcme) {return ["cmevla", _join(expr)];}
gismu = long_rafsi &stress &final_syllable vowel &post_word
fuhivla = fuhivla_head syllable &stress consonantal_syllable* final_syllable
cmavo = !cmevla !(CVC !stress y h? lujvo_core / CVC &stress y short_final_rafsi) (!h !(consonant consonant+) onset (nucleus h)* nucleus / y+) &post_word

lujvo_core = ( expr:(hy_rafsi / fuhivla_rafsi / y_rafsi / !any_fuhivla_rafsi y_less_rafsi !any_fuhivla_rafsi) {return [_join(expr),"-"];} )* (expr:(fuhivla / gismu_CVV_final_rafsi) {return [_join(expr)];} / (expr:((stressed_hy_rafsi / stressed_fuhivla_rafsi / stressed_y_rafsi / CVC_CCV_CVV &stress)) exp:(short_final_rafsi)) {return [_join(expr),"-",_join(exp)];} )

any_fuhivla_rafsi = fuhivla / fuhivla_rafsi / stressed_fuhivla_rafsi
rafsi_string = y_less_rafsi* (gismu_CVV_final_rafsi / CVC_CCV_CVV &stress !y short_final_rafsi / y_rafsi / stressed_y_rafsi / (CVC_CCV_CVV &stress !y)? initial_pair y / hy_rafsi / stressed_hy_rafsi)

fuhivla_head = !rafsi_string !cmavo !(!rafsi_string consonant rafsi_string) !h &onset unstressed_syllable*
zifcme = !h (nucleus / glide / h / consonant !pause)* consonant &pause

stressed_fuhivla_rafsi = expr:(fuhivla_head syllable &stress consonantal_syllable* onset) y {return [_join(expr),""];}//if onset is ' then dont return it
fuhivla_rafsi = expr:(&unstressed_syllable fuhivla_head onset) y h? {return [_join(expr),""];}

stressed_y_rafsi = expr:(long_rafsi / CVC) &stress y {return [_join(expr),""];}
y_rafsi = expr:(long_rafsi / CVC ) !stress y h? {return [_join(expr),""];}

y_less_rafsi = !y_rafsi !stressed_y_rafsi !hy_rafsi !stressed_hy_rafsi CVC_CCV_CVV !stress !y !h

stressed_hy_rafsi = expr:((long_rafsi vowel / CVC_CCV_CVV )) &stress h y {return [_join(expr),""];}
hy_rafsi = expr:(long_rafsi vowel / CVC_CCV_CVV ) !stress h y h? {return [_join(expr),""];}

CVC = CV consonant
CVC_CCV = CVC / ini_vwl
ini_vwl = initial_pair vowel

CVC_CCV_CVV = CVC_CCV / consonant (vowel !stress h vowel / diphthong) (r &consonant / n &r)?
gismu_CVV_final_rafsi = gismu / CV &stress h &final_syllable vowel &post_word
short_final_rafsi = &final_syllable (consonant diphthong / ini_vwl) &post_word

unstressed_syllable = syllable !stress / consonantal_syllable

long_rafsi = CVC_CCV consonant

CV = consonant vowel

final_syllable = onset !y nucleus !cmevla &post_word
stress = (consonant / glide)* h? y? syllable pause
any_syllable = onset nucleus coda? / consonantal_syllable
syllable = onset !y nucleus coda?
consonantal_syllable = consonant &syllabic coda

coda = !any_syllable consonant &any_syllable / syllabic? consonant? &pause
onset = h / glide / (affricate / (cs !x / jz !(n / l / r))? (pfbgvkx / (t / d / n !r) !l / m)? (l / r)?) !consonant !glide
nucleus = vowel / diphthong / y !nucleus
glide = (i / u) &nucleus !glide
diphthong = ([a] u !u / [aeo] i !i) !nucleus
vowel = [aeiou] !nucleus

i = [i]
u = [u]
y = [y] !(!y nucleus)

initial_pair = &onset consonant consonant !consonant

affricate = t cs / d jz

consonant = pfbgvkx / d / jz / cs / t / syllabic

syllabic = l / m / n / r

l = [l] !glide
m = [m] !glide
n = [n] !glide !affricate
r = [r] !glide
pfbgvkx = [pfbgvkx] !glide
d = [d] !glide
jz = [jz] !glide
cs = [cs] !glide
x = [x] !glide
t = [t] !glide
h = ['] &nucleus

post_word = pause / !nucleus lojban_word

pause = [.\t\n\r?!\u0020]+ / !.